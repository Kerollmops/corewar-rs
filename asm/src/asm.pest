// ugly comments everywhere
asm = { soi ~ comment* ~ (prog_header) ~ (comment | identifier)* ~ eoi }

prog_header = { (prog_name) ~ (prog_comment) ~ prog_extend? }

// pest autorules
comment = _{ whitespace* ~ ("#" | ";") ~ (!"\n" ~ any)* ~ "\n" }
whitespace = _{ " " | "\t" | "\r" | "\n" }

quotted_string = @{ "\"" ~ (!"\"" ~ any)* ~ "\"" }
prog_name = { ".name" ~ (quotted_string) }
prog_comment = { ".comment" ~ (quotted_string) }
prog_extend = { ".extend" }

identifier = { (label_decl | instr) }

label = { ('a'..'z' | '0'..'9' | "_")+ }
label_decl = @{ (label) ~ ":" }
label_name = @{ ":" ~ (label) }

instr = { (live |
           load |
           store |
           addition |
           substraction |
           and |
           or |
           xor |
           zjump |
           loadindex |
           storeindex |
           fork |
           longload |
           longloadindex |
           longfork |
           display) }

live = { "live" ~ (direct) }
load = { "ld" ~ (direct | indirect) ~ "," ~ (register) }
store = { "st" ~ (register) ~ "," ~ (indirect | register) }
addition = { "add" ~ (register) ~ "," ~ (register) ~ "," ~ (register) }
substraction = { "sub" ~ (register) ~ "," ~ (register) ~ "," ~ (register) }
and = { "and" ~ (direct | indirect | register) ~ "," ~ (direct | indirect | register) ~ "," ~ (register) }
or = { "or" ~ (direct | indirect | register) ~ "," ~ (direct | indirect | register) ~ "," ~ (register) }
xor = { "xor" ~ (direct | indirect | register) ~ "," ~ (direct | indirect | register) ~ "," ~ (register) }
zjump = { "zjmp" ~ (direct) }
loadindex = { "ldi" ~ (direct | indirect | register) ~ "," ~ (direct | register) ~ "," ~ (register) }
storeindex = { "sti" ~ (register) ~ "," ~ (direct | indirect | register) ~ "," ~ (direct | register) }
fork = { "fork" ~ (direct) }
longload = { "lld" ~ (direct | indirect) ~ "," ~ (register) }
longloadindex = { "lldi" ~ (direct | indirect | register) ~ "," ~ (direct | register) ~ "," ~ (register) }
longfork = { "lfork" ~ (direct) }
display = { "aff" ~ (register) | "disp" ~ (register) } // reduce this

// urgh! ugly "comment?"
register = @{ "r" ~ (number) ~ comment? }
direct = @{ "%" ~ (hexnumber | number | "-" ~ number | label_name) ~ comment? }
indirect = @{ (hexnumber | number | "-" ~ number | label_name)  ~ comment? }

number = @{ ('0'..'9')+ }
hexnumber = @{ "0x" ~ ('0'..'9' | 'a'..'f' | 'A'..'F')+ }
