// ugly comments everywhere
asm = { soi ~ comment* ~ props ~ (comment | label_decl | instr)* ~ eoi }

prop_name = @{ 'a'..'z'+ }
prop_value = ${ quotted_string }
prop = ${ "." ~ prop_name ~ (space+ ~ prop_value)? }
props = ${ (whitespace* ~ prop ~ new_line)* }

// pest autorules
comment = _{ whitespace* ~ "#" ~ (!"\n" ~ any)* ~ "\n" }
new_line = _{ "\r\n" | "\n" }
space = _{ " " | "\t" }
whitespace = _{ space | new_line }

inner_string = @{ (!"\"" ~ any)* }
quotted_string = ${ "\"" ~ inner_string ~ "\"" }

label_name = @{ ('a'..'z' | '0'..'9' | "_")+ }
label_decl = ${ label_name ~ ":" }
label_call = ${ ":" ~ label_name }

instr_name = @{ 'a'..'z'+ }
instr_name_space = ${ instr_name ~ whitespace }
instr = { instr_name_space ~ (parameter ~ ",")* ~ parameter }

parameter = { direct | indirect | register }

register = ${ "r" ~ number }
direct = ${ "%" ~ (hexnumber | "-"? ~ number | label_call) }
indirect = ${ (hexnumber | "-"? ~ number | label_call) }

number = @{ '0'..'9'+ }
hexnumber = @{ "0x" ~ ('0'..'9' | 'a'..'f' | 'A'..'F')+ }
